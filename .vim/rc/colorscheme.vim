""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"
" colorscheme.vim
"
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" colorscheme edit
" molokaiに近づける
" autocmd ColorSchemeをつけるとnvimに影響を及ぼすからつけない
highlight Comment ctermfg=59
highlight Function ctermfg=118
highlight Identifier ctermfg=208 cterm=none
highlight Constant ctermfg=135 cterm=bold
highlight String ctermfg=142
highlight Special ctermfg=81
highlight Statement ctermfg=161 cterm=bold
highlight Type ctermfg=81 cterm=none
highlight PreProc ctermfg=118

" For syntax group get
function! s:get_syn_id(transparent)
    let synid = synID(line("."), col("."), 1)
    if a:transparent
        return synIDtrans(synid)
    else
        return synid
    endif
endfunction

function! s:get_syn_attr(synid)
    let name = synIDattr(a:synid, "name")
    let ctermfg = synIDattr(a:synid, "fg", "cterm")
    let ctermbg = synIDattr(a:synid, "bg", "cterm")
    let guifg = synIDattr(a:synid, "fg", "gui")
    let guibg = synIDattr(a:synid, "bg", "gui")
    return {
        \ "name": name,
        \ "ctermfg": ctermfg,
        \ "ctermbg": ctermbg,
        \ "guifg": guifg,
        \ "guibg": guibg}
endfunction

function! s:get_syn_info()
    let baseSyn = s:get_syn_attr(s:get_syn_id(0))
    echo "name: " . baseSyn.name .
        \ " ctermfg: " . baseSyn.ctermfg .
        \ " ctermbg: " . baseSyn.ctermbg .
        \ " guifg: " . baseSyn.guifg .
        \ " guibg: " . baseSyn.guibg
    let linkedSyn = s:get_syn_attr(s:get_syn_id(1))
    echo "link to"
    echo "name: " . linkedSyn.name .
        \ " ctermfg: " . linkedSyn.ctermfg .
        \ " ctermbg: " . linkedSyn.ctermbg .
        \ " guifg: " . linkedSyn.guifg .
        \ " guibg: " . linkedSyn.guibg
endfunction

command! SyntaxInfo call s:get_syn_info()

